package com.gb.puremvc.model 
{
    import com.gb.puremvc.model.enum.GBNotifications;
    
    import org.puremvc.as3.multicore.utilities.startupmanager.interfaces.IStartupProxy;
    import org.puremvc.as3.multicore.utilities.startupmanager.model.StartupResourceProxy;
  
	/**
	 * Base class for <code>Proxy</code> instances used by the PureMVC Startup Manager.
	 */
    public class EntityProxy extends AbstractProxy implements IStartupProxy
    {
		private var _proxyNameSR:String;
		
		/**
		 * Constructor.
		 */		
        public function EntityProxy(proxyName:String = null, data:Object = null) 
		{
            super(proxyName, data);
			
			_proxyNameSR = proxyName != null ? proxyName + "SR" : null;
        }

		public function load():void
		{
			sendLoadedNotification(GBNotifications.STARTUP_RESOURCE_LOADING, proxyName, proxyNameSR);
		}
		
        protected function sendLoadedNotification(noteName:String, noteBody:Object, srName:String):void 
		{
            var srProxy:StartupResourceProxy = facade.retrieveProxy(srName) as StartupResourceProxy;
            if (!srProxy.isTimedOut())
			{
				// passing StartupResource proxy name allows us to distinguish it
                sendNotification(noteName, noteBody, srName);
			}
        }
		
		/**
		 * Returns the unique proxy name used by the PureMVC Startup Manager.
		 * <p>
		 * This is created by appending "SR" to the specified <code>proxyName</code>.</p>
		 * 
		 * @returns 	the unique proxy name used by the PureMVC StartupManager
		 */
		public function get proxyNameSR():String { return _proxyNameSR; }
    }
}